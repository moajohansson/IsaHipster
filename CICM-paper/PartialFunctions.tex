Isabelle is a logic of total functions. Nonetheless, we can define
apparently partial functions, such as \verb|hd|:
\begin{verbatim}
fun hd :: "'a list => 'a" where
  "hd (x#xs) = x"
\end{verbatim}

How do we reconcile \verb|hd| being partial with Isabelle functions
being total? The answer is that in Isabelle, \verb|hd| is total, but
the behaviour of \verb|hd []| is unspecified: it returns some
arbitrary value of type \verb|'a|. Meanwhile in Haskell, \verb|head|
is partial, but the behaviour of \verb|head []| is specified: it
crashes. If we naively translate Isabelle's \verb|hd| into Haskell's
\verb|head|, we will not respect the semantics of Isabelle.

Hipster translates each Isabelle function into a semantically
equivalent, total Haskell function. We do this by adding
a \emph{default argument} that specifies the return value when no case
matches. For example, we will translate \verb|hd| into:
\begin{verbatim}
hd :: a -> [a] -> a
hd _ (x:xs) = x
hd def [] = def
\end{verbatim}
To model the notion that the default arguments are unspecified,
whenever we evaluate a test case we will pick a \emph{random} value
for the default argument. This value will vary from test case to test
case but will be consistent within one run of a test case.

Though the idea is simple, there are a few details to get right.
Suppose we define \verb|second|, which returns the second element of a list, as
\begin{verbatim}
second (x#y#xs) = y
\end{verbatim}
It might seem that we should translate \verb|second|, by analogy with \verb|hd|, as
\begin{verbatim}
second :: a -> [a] -> a
second def (x:y:xs) = y
second def _ = def
\end{verbatim}
but this translation is \emph{wrong}! If we call \verb|second| on
several lists of length 1, it will return the same value---the default
argument---in all cases. This does not respect the semantics of \verb|second|
in Isabelle, which returns an unspecified value whenever its argument
has length 1, but might return a different value for different arguments.
The default argument must therefore be a random \emph{function}
of the same type as the original function, which we call whenever the
original function is unspecified. Thus we really have:\footnote{We lied
about the type of \texttt{hd} above.}
\begin{verbatim}
second :: ([a] -> a) -> [a] -> a
second def (x:y:xs) = y
second def xs = def xs
\end{verbatim}

Another difficulty is what to do about functions that \emph{call} partial functions.
Suppose we define \verb|nth|, which indexes into a list, as
\begin{verbatim}
fun nth :: "nat => 'a list => 'a list" where
  "nth 0 xs = hd xs"
| "nth (Suc n) xs = nth n (tl xs)"
\end{verbatim}

\verb|nth| has no missing cases, so we might want to translate it as
\begin{verbatim}
nth :: Int -> [a] -> [a]
nth 0 xs = hd ??? xs
nth (n+1) xs = nth n (tl ??? xs)
\end{verbatim}
but what should we write for \verb|???|? We need default arguments to
pass to \verb|hd| and \verb|tl|, so \verb|nth| had better receive
them as arguments. Since our functions may be mutually recursive in
arbitrarily complex ways, the easiest way is to define a record
containing default arguments for \emph{all} the functions in our
program, and pass this record to every function, regardless of whether
it has missing cases or not. Thus our \verb|nth| example will become:\footnote{For
technical reasons, the default implementations in reality are not fully
polymorphic but have typeclass constraints related to random generation.}
\begin{verbatim}
data Default = Default {
  default_hd :: forall a. [a] -> a,
  default_tl :: forall a. [a] -> [a]
  }

hd :: Default -> [a] -> a
hd _ (x:xs) = x
hd def xs = default_hd def xs

tl :: Default -> [a] -> [a]
tl _ (x:xs) = xs
tl def xs = default_tl def xs

nth :: Default -> Int -> [a] -> [a]
nth def 0 xs = hd def xs
nth (n+1) xs = nth def (tl def xs)
\end{verbatim}
Here, \verb|hd| and \verb|tl| extract their default implementations
from the record, while \verb|nth| simply passes the record to \verb|hd|
and \verb|tl| and into the recursive call. The regularity of this
transformation makes it easy to implement.

It is quite easy to import this transformed program into QuickSpec.
We give QuickSpec the transformed functions as they are (including the
default argument) as well as one variable \verb|def| of
type \verb|Default|. The generated equations will mention \verb|def|:
for example, with a richer signature also containing
\verb|length| and \verb|append| we would get
\verb|nth def (length def xs) (append def xs ys) = hd def ys|.
To turn this into an equation suitable for Isabelle we simply erase
every occurrence of \verb|def|, to get
\verb|nth (length xs) (append xs ys) = hd ys|.
This erasure is sound because, if the Haskell equation holds
regardless of which default values we choose, then the Isabelle
equation, in which the default values are unspecified, must also hold.
